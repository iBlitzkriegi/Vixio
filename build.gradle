group 'me.iblitzkriegi.vixio'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'de.undercouch:gradle-download-task:3.3.0'
    }

}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'de.undercouch.download'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    jcenter()

    //Spigot API
    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    //LavaPlayer
    maven {
        url 'http://maven.sedmelluq.com'
    }

    //Bungeecord-chat (needed for Spigot)
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    //Skript API
    maven {
        url 'https://raw.githubusercontent.com/bensku/mvn-repo/master/'
    }

    //Skript's dependencies (to avoid errors when compiling)
    maven {
        url 'https://repo.destroystokyo.com/repository/maven-public/'
    }

    maven {
        url 'http://maven.sk89q.com/repo'
    }

    maven {
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        url 'https://jitpack.io'
    }

}


dependencies {

    // Spigot API
    shadow group: 'org.spigotmc', name: 'spigot-api', version: project.property('apiversion')

    // JDA
    compile 'net.dv8tion:JDA:3.5.0_327'

    // LavaPlayer
    compile 'com.sedmelluq:lavaplayer:1.2.45'

    // Skript API
    shadow group: 'ch.njol', name: 'skript', version: project.property('skriptapiversion')
    compile 'ch.qos.logback:logback-classic:1.2.3'

}

jar.enabled = false
assemble.dependsOn(shadowJar)

def pluginDir = 'src/main/resources/plugin.yml'
def tempDir = 'build/tmp/tempResources'


processResources.doFirst {
    copy {
        from(pluginDir)
        into(tempDir)
    }
    ant.replace(file: pluginDir, token: '@VERSION@', value: project.property('version'))
}

processResources.doLast {
    copy {
        from(tempDir + '/plugin.yml')
        into(project.file(pluginDir).parent)
    }
}

shadowJar {
    classifier = null
    destinationDir = new File(project.property('destinationDir'))

}